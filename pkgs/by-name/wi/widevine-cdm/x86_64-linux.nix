{
  lib,
  stdenv,
  fetchurl,
  go-crx3,
}:

stdenv.mkDerivation (finalAttrs: {
  # version, src.url, and src.hash generated by update-x86_64.py
  pname = "widevine-cdm";
  version = "4.10.2891.0";

  src = fetchurl {
    url = "https://edgedl.me.gvt1.com/edgedl/release2/chrome_component/aclxnidwwkj5di3vduduj2gqpgpq_4.10.2891.0/oimompecagnajdejgnnjijobebaeigek_4.10.2891.0_linux_b4hin3q5s66ws2322cyyfp35lu.crx3";
    hash = "sha512-8S/4FRQQmQAO9wJ7QGJg8KUVxUQgm5/oEl6l9N2/WGAdMCIqxBoBPrCkPadrpwQUpK0WYX2fIZ8Zzfx3lNm8lA==";
  };

  nativeBuildInputs = [ go-crx3 ];

  unpackPhase = ''
    unpackDir="src"
    cp "$src" "$unpackDir".crx # go-crx3 doesn't like .crx3 extensions
    crx3 unpack "$unpackDir".crx

    cd "$unpackDir"
  '';

  installPhase = ''
    runHook preInstall

    install -vD manifest.json $out/share/google/chrome/WidevineCdm/manifest.json
    install -vD LICENSE $out/share/google/chrome/WidevineCdm/LICENSE.txt
    install -vD _platform_specific/linux_x64/libwidevinecdm.so $out/share/google/chrome/WidevineCdm/_platform_specific/linux_x64/libwidevinecdm.so

    runHook postInstall
  '';

  passthru.updateScript = ./update-x86_64.py;

  meta = import ./meta.nix lib;
})
